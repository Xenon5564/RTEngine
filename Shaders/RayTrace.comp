#version 450
layout(local_size_x = 8, local_size_y = 8) in;

layout(rgba32f, binding = 0) uniform image2D destImage;
layout(rgba32f, binding = 1) uniform image2D accumImage;

layout(std140, binding = 2) uniform CameraUBO {
    vec3 camPos;
    float camFOV;
    vec3 camForward;
    int globalFrameID;
    vec3 camUp;
    int frameID;
    vec3 camRight;
} cam;

struct Sphere{
    vec3 position;
    float radius;
    vec3 albedo;
    float roughness;
    float metallic;
};

const uint SPHERE_COUNT = 5;
const float PI = 3.14159265358979323846;
const float EPSILON = 0.0001;

Sphere spheres[SPHERE_COUNT] = Sphere[](
    Sphere(vec3(-1.0, 0.0, 0.0), 0.5, vec3(1.0, 0.766, 0.336), 0.2, 1.0),
    Sphere(vec3(0.0, 0.0, 0.0), 0.5, vec3(0.95, 0.93, 0.88), 0.25, 0.0),
    Sphere(vec3(1.0, 0.0, 0.0), 0.5, vec3(0.3, 0.3, 0.8), 0.5, 0.0),
    Sphere(vec3(2.0, 0.0, 0.0), 0.5, vec3(1.0, 0.0, 0.0), 1.0, 0.2),
    Sphere(vec3(0.0, -100.5, 0.0), 100, vec3(1.0, 1.0, 1.0), 1.0, 0.0)
);

// Randomness
uint hash(uint x) {
    x ^= x >> 17;
    x *= 0xED5AD4BBu;
    x ^= x >> 11;
    x *= 0xAC4C1B51u;
    x ^= x >> 15;
    x *= 0x31848BABu;
    x ^= x >> 14;
    return x;
}
float rand(inout uint seed) {
    seed = hash(seed);
    return float(seed) / float(0xFFFFFFFFu); // Normalize to [0, 1)
}

// Samples cosine-weighted hemisphere oriented around `normal`
vec3 sampleHemisphere(vec3 normal, inout uint seed) {
    float u1 = rand(seed);
    float u2 = rand(seed);

    float r = sqrt(u1);
    float theta = 6.28318530718 * u2; // 2Ï€

    float x = r * cos(theta);
    float y = r * sin(theta);
    float z = sqrt(1.0 - u1);

    // Build tangent frame (TBN)
    vec3 tangent = normalize(abs(normal.x) > 0.1 ? cross(normal, vec3(0,1,0)) : cross(normal, vec3(1,0,0)));
    vec3 bitangent = cross(normal, tangent);

    return normalize(x * tangent + y * bitangent + z * normal);
}

// -------------------- Cook-Torrance Specular BRDF --------------------
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

float DistributionGGX(float NdotH, float roughness)
{
    float a  = roughness * roughness;
    float a2 = a * a;
    float denom = (NdotH * NdotH) * (a2 - 1.0) + 1.0;
    return a2 / (PI * denom * denom);
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = roughness + 1.0;
    float k = (r * r) / 8.0;
    return NdotV / (NdotV * (1.0 - k) + k);
}

float GeometrySmith(float NdotV, float NdotL, float roughness)
{
    return GeometrySchlickGGX(NdotV, roughness) * GeometrySchlickGGX(NdotL, roughness);
}

vec3 CookTorranceSpec(vec3 N, vec3 V, vec3 L, float roughness, vec3 F0)
{
    vec3 H = normalize(V + L);
    float NdotL = max(dot(N, L), 0.0);
    float NdotV = max(dot(N, V), 0.0);
    float NdotH = max(dot(N, H), 0.0);
    float VdotH = max(dot(V, H), 0.0);

    float D = DistributionGGX(NdotH, roughness);
    float G = GeometrySmith(NdotV, NdotL, roughness);
    vec3 F = fresnelSchlick(VdotH, F0);

    vec3 numerator = D * G * F;
    float denominator = max(4.0 * NdotV * NdotL, 0.001);
    return numerator / denominator * NdotL;
}

bool RaySphereIntersection(vec3 rayOrigin, vec3 rayDir, vec3 sphereCenter, float radius, out float t) {
    vec3 oc = rayOrigin - sphereCenter;
    float a = dot(rayDir, rayDir); // should be 1.0 if rayDir is normalized
    float b = 2.0 * dot(oc, rayDir);
    float c = dot(oc, oc) - radius * radius;
    float discriminant = b * b - 4.0 * a * c;

    if (discriminant < 0.0) {
        return false;
    }

    float sqrtDisc = sqrt(discriminant);
    float t0 = (-b - sqrtDisc) / (2.0 * a);
    float t1 = (-b + sqrtDisc) / (2.0 * a);

    // Return the nearest positive t
    if (t0 > EPSILON) {
        t = t0;
        return true;
    } else if (t1 > EPSILON) {
        t = t1;
        return true;
    }

    return false; // both intersections are behind the ray
}

bool CastRay(vec3 rayOrigin, vec3 rayDir, out Sphere hitSphere, out float t)
{
    float closestT = 100000000;
    float dis;
    int closestID = -1;

    for(int i = 0; i < SPHERE_COUNT; i++)
    {
        if(RaySphereIntersection(rayOrigin, rayDir, spheres[i].position, spheres[i].radius, dis))
        {
            if(dis < closestT)
            {
                closestT = dis;
                closestID = i;
            }
        }
    }

    if(closestID != -1)
    {
        hitSphere = spheres[closestID];
        t = closestT;
        return true;
    }

    return false;
}

vec3 TraceRay(vec3 camPos, vec3 camToPlaneDir, uint seed)
{
    vec3 rayOri = camPos;
    vec3 rayDir = camToPlaneDir;

    vec3 finalColor = vec3(0, 0, 0);
    vec3 pathThroughput = vec3(1, 1, 1); // Initial throughput

    float t;
    float dummyT;

    Sphere hitSphere;
    Sphere dummySphere;

    for(int b = 0; b < 5; b++)
    {
        if(CastRay(rayOri, rayDir, hitSphere, t))
        {
            vec3 sphereCen = hitSphere.position;
            vec3 sphereCol = hitSphere.albedo;
            float sphereRoughness = hitSphere.roughness;
            float sphereMetallic = hitSphere.metallic;

            vec3 lightPos = vec3(0.0, 5, 0.0);
            float lightStrength = 20.0;

            vec3 hitPoint = rayOri + rayDir * t;
            vec3 normal = normalize(hitPoint - sphereCen);

            hitPoint += normal * EPSILON; // Offset to avoid self-intersection

            rayOri = hitPoint; // Update ray origin to the hit point

            if(sphereRoughness <= rand(seed)) // Check if we should sample a new direction
            {
                rayDir = sampleHemisphere(normal, seed); // Sample a new direction in the hemisphere
            }
            else
            {
                rayDir = reflect(rayDir, normal); // Reflect the ray direction
            }

            vec3 lightDir = normalize(lightPos - hitPoint);
            vec3 viewDir = normalize(-rayDir);
            vec3 F0 = mix(vec3(0.04), sphereCol, sphereMetallic);
            vec3 H = normalize(viewDir + lightDir);
            vec3 fresnel = fresnelSchlick(max(dot(viewDir, H), 0.0), F0);
            vec3 kS = fresnel;
            vec3 kD = (vec3(1.0) - kS) * (1.0 - sphereMetallic);
            
            if(!CastRay(hitPoint, lightDir, dummySphere, dummyT))
            {
                float lambert = max(0.0, dot(normal, lightDir));
                float lightDis = length(lightPos - hitPoint);
                float lightAtten = lightStrength / (lightDis * lightDis);
                vec3 spec = CookTorranceSpec(normal, viewDir, lightDir, sphereRoughness, F0);

                vec3 diffuse = kD * sphereCol * lambert / PI;

                finalColor += pathThroughput * (diffuse + spec) * lightAtten;
            }
            pathThroughput *= kD * sphereCol / PI;
        }
        else
        {
            vec3 skyCol = vec3(0.1, 0.1, 0.1);
            finalColor += pathThroughput * skyCol; // Background color
            break;
        }
    }       
    return finalColor;
}

void main() {
    ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imgSize = imageSize(destImage);
    vec2 uv = (vec2(pix) + vec2(0.5)) / vec2(imgSize);

    uint seed = uint(pix.x * 73856093u ^ pix.y * 19349663u ^ uint(cam.globalFrameID * 1000.0)) * uint(cam.frameID + 2);

    float planeDist = 1;
    float halfHeight = tan(radians(cam.camFOV * 0.5)) * planeDist;
    float aspect = float(imgSize.x) / float(imgSize.y);
    float halfWidth = halfHeight * aspect;

    vec3 planeCenter = cam.camPos + cam.camForward * planeDist;
    vec3 pointOnPlane = planeCenter
        + (uv.x - 0.5) * 2.0 * halfWidth * cam.camRight
        + (uv.y - 0.5) * 2.0 * halfHeight * cam.camUp;

    vec3 dir = normalize(pointOnPlane - cam.camPos);

    vec3 prevAverageColor = imageLoad(accumImage, pix).rgb; // Load the accumulated color
    float totalFrames = float(cam.frameID) + 1.0;
    vec3 newAverage = (prevAverageColor * float(cam.frameID) + TraceRay(cam.camPos, dir, seed)) / totalFrames;

    imageStore(accumImage, pix, vec4(newAverage, 1.0)); // Store the new average color
    imageStore(destImage, pix, vec4(newAverage, 1.0));
    imageStore(destImage, pix, vec4(newAverage, 1.0));

}